# Maintainer: Nico <d3sox at protonmail dot com>
pkgname=nerd-fonts-sf-mono
pkgver=2.1.0
pkgrel=1
pkgdesc="Apple's San Francisco Mono Font, pathced with Nerd Font's font-patcher"
arch=('any')
url='https://developer.apple.com/fonts/'
license=('custom')
makedepends=('git' 'p7zip' 'python' 'fontforge' 'subversion')
conflicts=('nerd-fonts-sf-mono')
provides=('nerd-fonts-sf-mono')
source=('https://developer.apple.com/design/downloads/SF-Mono.dmg' 
        "https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/font-patcher"
        "svn+https://github.com/ryanoasis/nerd-fonts/tags/v$pkgver/src/glyphs")
sha256sums=('d91ed5d03a249b515dd1ba9aba7127dcd85bd1c7d2a3a7687531524b6d9a6a6d'
            'd5558cd419c8d46bdc958064cb97f963d1ea793866414c025906ec15033512ed'
            'SKIP')

build() {
  # prepare glyphs so that font-patcher can find them
  mkdir -p "$srcdir/src"
  ln -sf "$srcdir/glyphs" "$srcdir/src/glyphs"
  # extract fonts
  cd "$srcdir"
  7z x -y SF-Mono.dmg
  7z x -y "SFMonoFonts/SF Mono Fonts.pkg"
  7z x -y "Payload~"
  # patch fonts
  mkdir -p "$srcdir/patched"
  warning "Now patching all fonts. This will take very long..."
  for f in "$srcdir/Library/Fonts"/*.otf; do
    msg "Now patching $f"
    # patch font quiet with complete 
    # single-width glyphs
    python "$srcdir/font-patcher" -q -c -s -out "$srcdir/patched" "$f" &> /dev/null
    # double-width glyphs
    python "$srcdir/font-patcher" -q -c    -out "$srcdir/patched" "$f" &> /dev/null
  done
}

package() {
  cd "$srcdir/patched"
  install -d "$pkgdir/usr/share/fonts/apple"
  install -m644 *.otf "$pkgdir/usr/share/fonts/apple/"
}
